---
title : Nestedness significance testing
author : Timothée Poisot
date : 11th April 2018
layout: default
---

```julia; echo=false; results="hidden"
using EcologicalNetwork
using StatPlots
plotly()
```

The purpose of this case study is to illustrate how we can use the package to
perform significance testing. We will see how we can compare the measured value
of nestedness to random values derived from network permutations.

We start by loading a network, in this case the one by Fonseca & Ganade (1996).
This is a bipartite network.

```julia
N = fonseca_ganade_1996()
```

We will start by getting rid of the quantitative information. This is done by
converting our network to another type:

```julia
M = convert(BipartiteNetwork, N)
```

We will now measure the nestedness of the network, using the $\eta$ measure:

```julia
n0 = η(M);
```

This returns a dictionary with a value for the rows, the columns, and the entire
network.

```julia
n0
```

At this point, we will need to generate a few random matrices to test our
empirical measure against. We will focus on the Type II null model, in which the
probability of an interaction between two species is proportional to their
relative degrees. We will call this probabilistic template `T`:

```julia
T = null2(M)
```

This function will return another network, this time a probabilistic one.

```julia
typeof(T)
```

We can draw a random sample based on this template:

```julia
rand(T)
```

Of course, we would like to generate a larger sample -- so we can draw many
replicates at once:

```julia
random_draws = rand(T, 5000)
```

Some of these networks may be *degenerate*, *i.e.* they have species without
interactions. It is safer to remove them from our sample:

```julia
valid_draws = filter(x -> !isdegenerate(x), random_draws)
```

We are left with a smaller number of networks, but all of them have species
with at least one interaction. At this point, we can measure the nestedness
on all of these networks:

```julia
n_prime = map(x -> η(x)[:network], valid_draws)
```

We can also express most of this analysis as a single pipeline:

```julia
n_prime = N |>
    # Step 1 - remove interaction strength
    n -> convert(BipartiteNetwork, n) |>
    # Step 2 - generate the probability template
    null2 |>
    # Step 3 - draw random networks
    n -> rand(n, 40000) |>
    # Step 4 - remove degenerate networks
    n -> filter(x -> !isdegenerate(x), n) .|>
    # Step 5 - measure the nestedness
    n -> η(n)[:network]
```

This notation is more compact, and can help understand the flow of the analysis
better.

We can now figure out the *p*-value for this test (specifically, is the network
*less* nested than expected by chance), by measuring the proportion of randon
draws that are smaller than the empirical value:

```julia
p = sum(n_prime .<= n0[:network])./length(n_prime)
println("p ≈ $(round(p,3)), n = $(length(n_prime))
the network is $(p < 0.05 ? "less nested than" : "as nested as") expected by chance.")
```

Take note that the number of random draws used for this example (`j
length(n_prime)`) is **not enough** in most cases. Aim for anything above 10000.
Finally, we can also look at the distribution of these values (all of this code
is intended to make the final plot more palatable):

```julia
low, high = quantile(n_prime, [0.05, 0.95])
rectangle(w, h, x, y) = Shape(x + [0,w,w,0], y + [0,0,h,h])
pl = histogram(n_prime, fill=:lightgrey, c=:grey, lc=:grey, lw=1, framestyle=:zerolines, lab="Random draws", size=(900,300));
vline!(pl, [n0[:network]], lab="Empirical value", c=:black, ls=:dot);
plot!(pl, rectangle(low,120,0,0), opacity=.2, c=:orange, lab="Significance threshold", lw=0, lc=:orange);
plot!(pl, rectangle(0.4-high,120,high,0), opacity=.2, c=:orange, lab="", lw=0);
histogram!(pl, n_prime, fill=:lightgrey, c=:grey, lc=:grey, lw=1, lab="");
xaxis!(pl, (0.1, 0.4));
yaxis!(pl, (0.0, 120.0));
pl
```
