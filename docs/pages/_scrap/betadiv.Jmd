---
title : Network β-diversity
author : Timothée Poisot
date : 11th April 2018
layout: default
---

```julia; echo=false; results="hidden"
using EcologicalNetwork
using StatPlots
pyplot()
```

In this case study, we will compare the species composition and interaction
composition of a collection of networks:

```julia
all_of_wol = web_of_life()
eurasia_rodents = filter(x -> contains(x[:Reference], "Hadfield"), all_of_wol)
rodents_id = getfield.(eurasia_rodents, :ID)
rodents_networks = web_of_life.(rodents_id)
N = map(n -> convert(BinaryNetwork, n), rodents_networks)
```

## Is there strong global β-diversity?

For this first question, we will compare each network to its relevant part in
the metaweb -- this offers a way to measure how different each *measured*
network is compared to what we would expect knowing all regional interactions.

```julia
metaweb = reduce(union, N)
```

The function to measure the difference between interactions between shared
species is `βos`: we can apply it to an array of network using the `.` syntax.

We can pipe the output all the way to a plot:

```julia
os_prime = βos.(N, metaweb) .|> whittaker
histogram(os_prime)
```

This analysis reveals that most networks are reasonably close to what we expect
at the regional scale. We can identify the *most* dissimilar network:

```julia
most_dissimilar = first(find(os_prime .== maximum(os_prime)))
N[most_dissimilar]
```

## Pairwise comparison between networks

In the next step, we will compare the dissimilarity of interactions with the
dissimilarity of species compositions:

```julia
Ds = zeros(Float64, (length(N), length(N)))
Di = zeros(Float64, (length(N), length(N)))
Dw = zeros(Float64, (length(N), length(N)))
for i in eachindex(N[1:(end-1)])
  for j in (i+1):length(N)
    Ds[i,j] = sorensen(βs(N[i], N[j]))
    Di[i,j] = sorensen(βos(N[i], N[j]))
    Dw[i,j] = sorensen(βwn(N[i], N[j]))
  end
end
```

We can represent this, with white points showing `βos` (interactions between
shared species), and black points showing `βwn` (total interactions):

```julia
pl = scatter(Ds, Di, leg=false, mc=:white, xlab="Species dissimilarity",
  ms=4, aspectratio=1, framestyle=:zerolines, ylab="Network dissimilarity")
scatter!(pl, Ds, Dw, mc=:black, ms=4)
pl
```

This illustrate a common result: the similarity of species composition is not a
good predictor of the fact that these species will interact in the same way!
